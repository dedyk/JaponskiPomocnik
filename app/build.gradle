apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("app/signing.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    buildFeatures {
        dataBinding true
        buildConfig = true

        // for view binding:
        // viewBinding true
    }

    // uwaga jesli kiedys w przyszlosci ustawisz targetSdkVersion na 30 lub wiecej to prawdopodobnie przestanie dzialac opcja
    // android:requestLegacyExternalStorage="true", ktore jest wymagane dla Android 10+
    // trzeba bedzie to zmienic i sprawdzic, czy aplikacja dziala

    defaultConfig {
        applicationId "pl.idedyk.android.japaneselearnhelper"

        minSdkVersion 17
        compileSdk 35
        targetSdkVersion 35
        buildToolsVersion = '35.0.0'

        multiDexEnabled true

        versionCode 813
        versionName "20250803"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            shrinkResources false
        }
    }


    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    namespace 'pl.idedyk.android.japaneselearnhelper'
    lint {
        abortOnError false
    }

    gradle.taskGraph.afterTask { Task task, TaskState state ->
	if (state.failure) {
    	    // noop

	} else {

	    def versionName = defaultConfig.versionName
	    def versionCode = defaultConfig.versionCode

	    def outputRelease = "release/release_" + versionCode + "_" + versionName
	    def outputName = "JapaneseAndroidLearnHelper_release_" + versionCode + "_" + versionName

	    if (task.name == 'assembleRelease') {

		mkdir("../" + outputRelease)

		copy {
		    from('build/outputs/apk/release') {
			include 'app-release.apk'
		    }
		    into '../' + outputRelease
		    rename ("app-release.apk", outputName + ".apk")
		}
	    }

	    if (task.name == 'bundleRelease') {

		mkdir("../" + outputRelease)

		copy {
		    from('build/outputs/bundle/release') {
			include 'app-release.aab'
		    }
		    into '../' + outputRelease
		    rename ("app-release.aab", outputName + ".aab")
		}
	    }

	}
    }
}

dependencies {
    // implementation 'com.google.android.gms:play-services:+'
    implementation "androidx.core:core-ktx:1.8.0"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation files('libs/JapaneseDictionaryAPI-1.0-SNAPSHOT.jar')
    implementation files('libs/JapaneseDictionaryLuceneCommon-1.0-SNAPSHOT.jar')
    implementation files('libs/javacsv-2.1.jar')
    implementation files('libs/lucene-analyzers-common-4.7.2.jar')
    implementation files('libs/lucene-core-4.7.2-without-SPIClassIterator-and-RamUsageEstimator.jar')
    implementation files('libs/lucene-suggest-4.7.2.jar')
}
